apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
// Signing
apply plugin: 'signing'
signing {
    sign configurations.archives
}
//http://weibeld.net/java/publish-to-maven-central.html
// Deploying
apply plugin: 'maven'

// Add Javadoc JAR and sources JAR to artifact
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


group = 'io.github.samuel-davis'
archivesBaseName = "nifi-api-client-java"
version = '1.0.3'


sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

install {
    repositories.mavenInstaller {
        pom.artifactId = 'nifi-api-client-java'
    }
}

task execute(type: JavaExec) {
    main = System.getProperty('mainClass')
    classpath = sourceSets.main.runtimeClasspath
}


dependencies {
    compile 'io.swagger:swagger-annotations:1.5.8'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okhttp:logging-interceptor:2.7.5'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'joda-time:joda-time:2.9.3'
    testCompile 'junit:junit:4.12'
}

// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            // Add required metadata to POM
            pom.project {
                name 'Nifi Api Client Java'
                packaging 'jar'
                description 'Easy to use api for NIFI REST api'
                url 'https://github.com/samuel-davis/ossrh'

                scm {
                    connection 'scm:git:git://github.com/samuel-davis/ossrh.git'
                    developerConnection 'scm:git:git@github.com:samuel-davis/ossrh.git'
                    url 'https://github.com/samuel-davis/ossrh'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'sam'
                        name 'Sam Davis'
                        email 'codehounded@gmail.com'
                        organization 'OpenSource'
                        organizationUrl 'https://samuel-davis.github.io'
                    }
                }
            }
        }
    }
}
